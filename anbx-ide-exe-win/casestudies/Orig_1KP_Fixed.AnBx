Protocol: Orig_1KP_Fixed

Types: 
	Agent C,M,a;
	Certified a;
	Number empty,Desc,Price,ID,NxNONCE;
	SymmetricKey HxRC,HxSALTC;
	SeqNumber TID,Auth;
	Function [Agent,Agent ->* Number] can

Definitions:
	IDC: hmac(can(C,a),HxRC);
	Common: Price,ID,TID,NxNONCE,IDC,hmac(Desc,HxSALTC);
	Clear: ID,TID,NxNONCE,hash(Common);
	Slip: Price,hash(Common),can(C,a),HxRC;
	EncSlip: {Slip}pk(a);
	# SigA: {hash(Auth,hash(Common))}inv(sk(a))
	# Proposed modification of iKP - Bugliesi-Modesti "AnBx - Security Protocols Design and Verification"
	SigA: {C,M,hash(Auth,hash(Common))}inv(sk(a))
	
Knowledge: 
	C: C,M,a;
	M: C,M,a;
	a: C,M,a;
	C,a share can(C,a);
	C,M agree Price,Desc

Actions:
	
# 1. Initiate 
	C -> M: HxSALTC,IDC
	
# 2. Invoice 
	M -> C: Clear
	
# 3. PayMnt
	C -> M: EncSlip
	
# 4. Auth-Request
	M -> a: Clear,hmac(Desc,HxSALTC),EncSlip
	
# 5. Auth-Response
	a -> M: Auth, SigA
	
# 6. Confirm 
	M -> C: Auth,SigA
	
Goals:

	# credit card secrecy and authorisation
	can(C,a) secret between C,a
	# a authenticates C on can(C,a)		# fail S1TC
	
	# global agreement
	# C authenticates M on Common
	# a authenticates M on Common
	# a authenticates C on Common
	# M authenticates C on Common	
	
	# the following goals fail S1CT if original SigA is used
	M authenticates a on Auth
	C authenticates a on Auth
	M authenticates a on Common,Auth
	C authenticates a on Common,Auth
	
	# secrets
	Desc secret between C,M
	# Auth secret between C,M,a		# fails S1TC