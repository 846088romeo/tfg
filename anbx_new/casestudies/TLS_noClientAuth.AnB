Protocol: TLS_noClientAuth

Types:
	Agent A,B,s;
	Number NA,NB,Sid,PA,PB,PMS;
	Function pk,sk;		# Agent -> PublicKey
	Function hash;		# Untyped -> Number
	Function prf;		# Number,Number,Number -> Number
	Function clientK,serverK;		# Number,Number,Number -> SymmetricKey

Knowledge:
	A: A,B,sk(s),hash,clientK,serverK,prf;
	B: B,pk(B),sk(s),inv(pk(B)),{B,pk(B)}inv(sk(s)),hash,clientK,serverK,prf;
	s: s,sk(s),inv(sk(s))

Actions:
	[A] *-> B: A,NA,Sid,PA
	B -> A: NB,Sid,PB,{B,pk(B)}inv(sk(s))
	A -> B: {PMS}pk(B),hash(NB,B,PMS),{|hash(prf(PMS,NA,NB),A,B,NA,NB,Sid,PA,PB,PMS)|}clientK(NA,NB,prf(PMS,NA,NB))
	B -> A: {|hash(prf(PMS,NA,NB),A,B,NA,NB,Sid,PA,PB,PMS)|}serverK(NA,NB,prf(PMS,NA,NB))

Goals:
	[A] *->* B: prf(PMS,NA,NB)
	B *->* [A]: prf(PMS,NA,NB)
	inv(pk(B)) secret between B
