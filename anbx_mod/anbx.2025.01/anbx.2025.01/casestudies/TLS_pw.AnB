Protocol: TLS_pw

Types:
	Agent A,B,s;
	Number NA,NB,Sid,PA,PB,PMS,empty;
	Function pk,sk;		# Agent -> PublicKey
	Function hash;		# Untyped -> Number
	Function prf;		# Number,Number,Number -> SymmetricKey
	Function clientK,serverK;		# Number,Number,SymmetricKey -> SymmetricKey
	Function pw;		# Agent,Agent ->* SymmetricKey

Knowledge:
	A: A,sk(s),B,hash,clientK,serverK,prf,pw(A,B);
	B: s,B,A,pk(B),sk(s),inv(pk(B)),hash,clientK,serverK,prf,empty,pw(A,B);
	s: s,B,sk,pk,sk(s),inv(sk(s))

Actions:
	s *->* B: {B,pk(B)}inv(sk(s))
	B -> A: empty
	A -> B: A,NA,Sid,PA
	B -> A: NB,Sid,PB,{B,pk(B)}inv(sk(s))
	A -> B: {PMS}pk(B),hash(NB,B,PMS),{|hash(prf(PMS,NA,NB),A,B,NA,NB,Sid,PA,PB,PMS)|}pw(A,B)
	B -> A: {|hash(prf(PMS,NA,NB),A,B,NA,NB,Sid,PA,PB,PMS)|}serverK(NA,NB,prf(PMS,NA,NB))

Goals:
	B authenticates A on prf(PMS,NA,NB)
	A authenticates B on prf(PMS,NA,NB)
	prf(PMS,NA,NB) secret between A,B
	pw(A,B) guessable secret between A,B
	inv(pk(B)) secret between B
	inv(sk(s)) secret between s
